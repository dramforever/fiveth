; Bootstrapping `define`: First, create `rawdef` in allot space
=here @ dup "rawdef" tuck dup =here +! pswap swap pswap memcpy align
; ( p:name n:name )

; As a reminder the structure is { code link name namelen }
=here @ 0 5 +n =here +!
=_runindirect over !
=last @ over 1 +n !
tuck 3 +n !
tuck 2 +n !

[
    ; ( p:name n:name p:code n:payload -- p:def )
    align =here @ swap
    ; ( p:name n:name p:code p:def n:payload -- )
    4 +n aligned =here +!
    ; ( p:name n:name p:code p:def -- )

    tuck !
    =last @ over 1 +n !
    tuck 3 +n !
    tuck 2 +n !

    dup =last !
] over 4 +n !

=last !

; Then we can `define` in terms of `rawdef`
"define" =_runindirect native rawdef
[
    ; ( p:name n:name l -- )
    nrot =_runindirect native rawdef
    4 +n !
] swap 4 +n !

"here" [ =here @ ] define
"allot" [ =here +! ] define

"setind" [
    find_word 4 +n !
] define

"nl" [ 10 b. ] define

"hello" [
    "Hello, world!" s. nl
] define

"hex_digit" [ ; ( num -- char )
    dup 10 < [ 48 ] [ 87 ] ifelse +
] define

"tohex" [ ; ( p:buf num -- n:buf )
    tuck [ 4 >>u swap 1 + swap dup ] repeat drop
    ; ( num p:end )
    dup nrot ; ( p:end num p:end )
    [
        1 -
        pdup swap 15 & hex_digit swap b!
        swap 4 >>u swap
        over
    ] repeat
    nip -
] define

"%u10" [ ; ( a -- a%10 )
    dup /u10 *10 -
] define

"todec_u" [ ; ( p:buf num -- n:buf )
    tuck [ /u10 swap 1 + swap dup ] repeat drop
    ; ( num p:end )
    dup nrot ; ( p:end num p:end )
    [
        1 -
        pdup swap %u10 48 + swap b!
        swap /u10 swap
        over
    ] repeat
    nip -
] define

"todec" [
    dup <0
    [
        over 45 swap b!
        swap 1 + swap
        neg todec_u
        1 +
    ] [
        todec_u
    ] ifelse
] define

"." [
    lhere 32 lallot
    dup rot todec s.
    32 b.
] define

".h" [
    lhere 32 lallot
    "0x" s.
    dup rot tohex s.
    32 b.
] define

"var" [ ; ( p:name n:name n:payload -- )
    =_runconst swap 1 +n rawdef
    dup 5 +n swap 4 +n !
] define

"_dump" [
    dhere =_dstack_start swap
    "[ " s.
    over - lgnative >>u
    [ over swap +n @ .h ] count
    drop
    "]" s.
] define

"words" [
    =last @
    [
        dup 1 neg =
        [
            drop 0
        ] [
            dup name_of_def s. " " s.
            1 +n @
            1
        ] ifelse
    ] repeat
] define

; Overridable definition
"define_ind" [ define ] define

"getchar_ready" [ 1 ] define_ind
"getchar_try" [ 1 neg ] define_ind

"getchar" [
    [ getchar_ready =0 ] repeat
    getchar_try
] define

"while" [ ; ( l:cond l:body -- )
    [
        l, l,
        1 l@ run
        0 l@ swap
        [ run 1 ] [ drop 0 ] ifelse
        1 l@ 0 l@ rot
    ] repeat
    pdrop
] define

"_showstep" [ ; ( depth current -- depth current )
  dup @
  begin_case
    [ dup `__lit = ] [ drop 1 +n dup @ . 1 +n ] case
    [ dup `__litlist = ] [ drop 2 +n "[ " s. swap 1 + swap ] case
    [ dup `__litstr = ] [
        drop dup 1 +n @ over 2 +n swap 34 b. s. 34 b. 32 b.
        dup 1 +n @ + 2 +n aligned
    ] case
    [ dup `__return = ] [ drop 1 +n "] " s. swap 1 - swap ] case
    [ 1 ] [ name_of_def s. " " s. 1 +n ] case
  end_case
] define

"_showlist" [
    1 swap
    "[ " s.
    [ _showstep over >0 ] repeat
    pdrop
] define

"showdef" [
    dup @
    begin_case
    [ dup =_rundef = ] [ drop 4 +n _showlist ] case
    [ dup =_runindirect = ] [ drop 4 +n @ "(indirect) " s. _showlist ] case
    [ 1 ] [ pdrop "(unknown)" s. ] case
    end_case
] define
